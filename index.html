<html>

<head>
<title>Mandelbox370</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<link href="css/ui-lightness/jquery-ui-1.10.4.custom.css" rel="stylesheet">
<script src="js/jquery-1.10.2.js"></script>
<script src="js/jquery-ui-1.10.4.custom.js"></script>
<script src="js/gl-matrix-min.js"></script>

<style type="text/css">
    #canvas-wrap { position: relative }
    #overlay { 
        position: absolute; 
        top: 0px; 
        left: 620px; 
        width: 260px;
        height: 300px;
    }
</style>

<script id="shader-3dbox" type="x-shader/x-fragment">
    precision mediump float;
    const int MAX_ITERS = 30;
    const int MAX_STEPS = 128;
    const float MAX_DIST = 50.0;
    
    // the texCoords passed in from the vertex shader.
    varying vec2 v_texCoord;

    uniform vec3 anchor;
    uniform vec3 cameraPos;
    uniform vec3 cameraLook;
    uniform vec3 cameraUp;
    uniform vec3 juliaboxConstant;
    uniform float scaleFactor;
    uniform float sliceLocation;
    uniform float epsilon;
    uniform int maxIterations;
    uniform bool juliaboxMode;
    uniform bool viewSlicingPlane;

    float distanceEstimate(vec3 rayPos, float constant1, float constant2)
    {
        // For the Juliabox, c is a constant. For the Mandelbox, c is variable.
        vec3 c = juliaboxMode ? juliaboxConstant : rayPos;
        vec3 v = rayPos;
        float dr = 1.0;

        for (int i = 0; i < MAX_ITERS; i++)
        {
            // GLSL doesn't support variable loop indices...
            if (i == maxIterations)
                break;

            // Box fold
            v = clamp(v, -1.0, 1.0) * 2.0 - v;

            // Sphere fold.
            float mag = dot(v, v);
            if (mag < 0.25)
            {
                v = v * 4.0;
                dr = dr * 4.0;
            }
            else if (mag < 1.0)
            {
                v = v / mag;
                dr = dr / mag;
            }

            v = v * scaleFactor + c;
            dr = dr * abs(scaleFactor) + 1.0;
        }

        return (length(v) - constant1) / dr - constant2;
    }

    int rayMarch(vec3 rayDir, inout float distance) {
        int stepCount = 0;

        float constant1 = abs(scaleFactor - 1.0);
        float constant2 = pow(float(abs(scaleFactor)), float(1 - maxIterations));

        for (int i = 0; i < MAX_STEPS; i++)
        {
            vec3 rayPos = cameraPos + rayDir * distance;
            float de = distanceEstimate(rayPos, constant1, constant2);

            // sphere
            // de = length(rayPos) - 0.99;

            distance += de * 0.95;
            stepCount++;

            if (de < epsilon)
                return stepCount;
            else if (distance > MAX_DIST)
                return 0;
        }

        return MAX_STEPS;
    }

    void main(void) 
    {
        // Calculate the unit vector representing the direction
        // from the camera to pixel (x, y) on the projection plane.
        vec3 w = normalize(cameraLook);
        vec3 u = normalize(cross(cameraUp, w));
        vec3 v = cross(w, u);

        vec2 coord = v_texCoord - 0.5;
        vec3 rayDir = w + u * coord.x + v * coord.y;

        float distance = 0.0;
        int steps = rayMarch(rayDir, distance);
        vec3 finalRayPos = cameraPos + distance * rayDir;

        bool crossedPlane = 
            (cameraPos.x < sliceLocation) != (finalRayPos.x < sliceLocation);

        // Base background color.
        vec4 color = vec4(0.5, 0.3, 0.7, 1.0);

        if (steps > 0)
        {
            // The (log(epsilon) * 2.0) offset is to compensate for the fact
            // that more steps are taken when epsilon is small.
            float adjusted = max(0.0, float(steps) + log(epsilon) * 2.0);
            float adjustedMax = float(MAX_STEPS) + log(epsilon) * 2.0;

            // Sqrt increases contrast.
            float distRatio = sqrt(adjusted / adjustedMax) * 0.8;
            color = vec4(vec3(1.0, 1.0, 1.0) * (1.0 - distRatio), 1.0);
        }

        // Draw transparent slicing plane if needed (there's no point in
        // drawing the slicing point for juliamode since it's not slicing it).
        if (crossedPlane && viewSlicingPlane && !juliaboxMode)
            color += vec4(-0.1, -0.1, 0.3, 0.0);

        // Draw anchor if needed.
        if (length(coord) < 0.01)
        {
            if (length(cameraPos - anchor) < distance)
                // Solid color if in front of fractal.
                color = vec4(1.0, 0.0, 0.0, 1.0);
            else
                // Transparent if behind fractal.
                color += vec4(0.5, -0.2, -0.2, 0.0);
        }

        gl_FragColor = color;
    }
</script>

<script id="shader-2dbox" type="x-shader/x-fragment">
    precision mediump float;

    const int MAX_ITERS = 30;

    uniform float scaleFactor;
    uniform float zoom2D;
    uniform float sliceLocation;
    uniform float escapeDistance;
    uniform int maxIterations;
    uniform bool juliaboxMode;
    uniform vec2 center;
    uniform vec3 juliaboxConstant;
    
    // the texCoords passed in from the vertex shader.
    varying vec2 v_texCoord;

    void main(void) 
    {
        int iterations = 0;
        float limit = escapeDistance * escapeDistance * 4.0;

        vec3 c = vec3((v_texCoord - 0.5) * zoom2D + center, sliceLocation);
        vec3 v = c;

        for (int i = 0; i < MAX_ITERS; i++) 
        {
            // GLSL doesn't support variable loop indices...
            if (i == maxIterations)
                break;

            // Box fold
            v = clamp(v, -1.0, 1.0) * 2.0 - v;

            // Sphere fold.
            float mag = dot(v, v);
            if (mag < 0.25)
                v = v * 4.0;
            else if (mag < 1.0)
                v = v / mag;

            v = scaleFactor * v + c;

            if (v.x * v.x > limit || v.y * v.y > limit || v.z * v.z > limit)
                break;

            iterations++;
        }

        if (iterations == maxIterations)
        {
            gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
        }
        else if (abs(c.x) > escapeDistance || abs(c.y) > escapeDistance ||
                 abs(c.z) > escapeDistance)
        {
            if (mod(floor(c.x) + floor(c.y), 2.0) < 1.0)
                gl_FragColor = vec4(1.0, 1.0, 1.0, 1.0);
            else
                gl_FragColor = vec4(0.8, 0.8, 0.8, 1.0);
        }
        else
        {
            gl_FragColor = vec4(vec3(
                0.7 + 0.3 * sin(float(iterations) * 0.7 + 0.5),
                0.7 + 0.3 * sin(float(iterations) * 0.9 + 0.1),
                0.7 + 0.3 * sin(float(iterations) * 0.5 + 0.3)),
                1.0);
        }

        // Draw a circle at the center of the point used as the constant for
        // the Juliabox, if needed.
        bool nearBoxConstant = length(c.xy - juliaboxConstant.xy) < zoom2D * 0.02;
        if (nearBoxConstant && juliaboxMode)
            gl_FragColor += vec4(0.5, -0.4, -0.4, 0.0);
    }
</script>

<script id="shader-fs-display" type="x-shader/x-fragment">
    precision mediump float;

    varying vec2 v_texCoord;
    uniform sampler2D sampler;

    void main(void) {
        gl_FragColor = texture2D(sampler, vec2(v_texCoord.s, v_texCoord.t));
    }
</script>

<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec2 position;
    uniform vec2 resolution;

    attribute vec2 texCoord;
    varying vec2 v_texCoord;

    void main(void) 
    {
        // Convert pi xels coordinates to range [0, 1]
        vec2 zeroToOne = position / resolution;
     
        // [0, 1] -> [-1, 1]
        vec2 clipSpace = zeroToOne * 2.0 - 1.0;
     
        gl_Position = vec4(clipSpace, 0, 1);

        // pass the texCoord to the fragment shader
        // The GPU will interpolate this value between points
        v_texCoord = texCoord;
    }
</script>

<script src="js/mandelbox370.js" type="text/javascript"></script>

</head>


<body onload="webGLStart();">
    <div id="canvas-wrap">
        <canvas id="mandelbox-canvas" style="border: none;" width="900" height="600"></canvas>
        <div id="overlay">
            <label for="slider-iterations-indicator">Iterations</label>
            <input type="text" id="slider-iterations-indicator" style="border:0; color:#f6931f; font-weight:bold;">
            <div id="slider-iterations"></div>

            <label for="slider-scale-indicator">Scale Factor</label>
            <input type="text" id="slider-scale-indicator" style="border:0; color:#f6931f; font-weight:bold;">
            <div id="slider-scale"></div>

            <label for="slider-slice-indicator">Slice Location</label>
            <input type="text" id="slider-slice-indicator" style="border:0; color:#f6931f; font-weight:bold;">
            <div id="slider-slice"></div>

            <label for="slider-accuracy-indicator">Accuracy</label>
            <input type="text" id="slider-accuracy-indicator" style="border:0; color:#f6931f; font-weight:bold;">
            <div id="slider-accuracy"></div>

            <br/>

            <input type="checkbox" id="toggle-juliabox"><label for="toggle-juliabox">View Juliabox</label>
            <input type="checkbox" id="toggle-slicer" checked><label for="toggle-slicer">View Slicing Plane</label>

            <!-- use with $( "#debuglabel" ).val( something ); -->
            <input type="text" id="debuglabel" style="border:0; color:#f6931f; font-weight:bold;">
        </div>
    </div>
    Double-click to change anchor (rotation center).
    <br/>
    Adjusting the accuracy will make the image darker, until you zoom in more (the more detailed the fractal, the more light it will trap).
    <br/>
    The checkboard has tiles of size 1, which gives a reference point for the size of the Mandelbox.
    <br/>
    In Juliabox mode, use the 2D view and the Slice Location to select the constant c.
</body>

</html>
